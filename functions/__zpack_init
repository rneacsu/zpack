#!/usr/bin/env zsh

# Output Functions
function __zpack_out() {
    print -P "%B%F{green}\u276F %f%b""$*"
}

function __zpack_out_plain() {
    print "$*"
}

function __zpack_warn() {
    print -P %F{yellow}"$*"%f >&2
}

function __zpack_err() {
    print -P %F{red}"$*"%f >&2
}

function __zpack_subcmd() {
    local prefix=$1
    local subcmd=$2

    if [[ -z $prefix ]]; then
        __zpack_err "No prefix given."
        return 1
    fi
    if [[ -z $subcmd ]]; then
        __zpack_err "No subcommand given."
        return 1
    fi
    if [[ ${+functions[__zpack_${prefix}_${subcmd}]} == 0 ]]; then
        __zpack_err "Invalid subcomand given."
        return 1
    fi
    shift 2
    __zpack_${prefix}_${subcmd} $@
}

function __zpack_apply() {
    unfunction __zpack_apply

    if [[ -n $ZPACK_CACHE ]]; then
        unset ZPACK_CACHE
        source $ZPACK_CACHE_FILE
        ZPACK_STATS[cache_hit]=1
    fi
    __zpack_completion apply
    ZPACK_APPLIED=1
}

# Init function
function __zpack_init() {
    unfunction __zpack_init

    if ! zmodload zsh/datetime; then
        __zpack_err "Could not load zsh/datetime module."
        return 1
    fi

    typeset -gA ZPACK_STATS
    ZPACK_STATS[init_start]=$EPOCHREALTIME

    # Check cache
    if [[ -f $ZPACK_CACHE_FILE && $ZPACK_CACHE_FILE -nt ${ZDOTDIR:-$HOME}/.zshrc ]]; then
        ZPACK_CACHE=1
    else
        autoload -Uz __zpack_cache

        # Init Variables
        ZPACK_PLUGINS=()
        ZPACK_RELEASES=()
        ZPACK_SNIPPETS=()
        ZPACK_BUNDLES=()
        ZPACK_BINARIES=()

        # Setup directories and files
        local dir
        for dir in $ZPACK_BIN_DIR $ZPACK_CACHE_DIR; do
            [[ -d $dir ]] || mkdir -p $dir
        done
        rm -f $ZPACK_COMPDUMP_PATH
        : > $ZPACK_CACHE_FILE
        __zpack_cache txt "#!/usr/bin/env zsh"
        __zpack_cache stats_time cache_start
        __zpack_cache path $ZPACK_BIN_DIR

        # Zsh Plugin Standard
        PMSPEC=0fbis
        __zpack_cache var PMSPEC
        __zpack_cache eval "typeset -ga zsh_loaded_plugins"
    fi

    # Define commands and their description
    typeset -gA __ZPACK_CMDS
    __ZPACK_CMDS=(
        "load"    "clone and load plugin"
        "omz"     "load ohmyzsh plugins or libraries"
        "bin"     "clone and add files to PATH"
        "release" "download and add binary release"
        "snippet" "download and load snippet from url"
        "bundle"  "load predefined bundles"
        "apply"   "apply setup to current shell"
        "update"  "update all repositories, reset and restart shell"
        "compile" "compile files from the given path"
        "clone"   "clone plugin from repository"
        "list"    "print plugins, releases, snippets, bundles and binaries"
        "reset"   "delete cache and bin dirs and restart shell"
        "prune"   "delete everything and restart shell"
        "stats"   "print statistics about zpack"
        "help"    "print usage information"
    )

    # Init completion
    autoload -Uz __zpack_completion && __zpack_completion init

    ZPACK_STATS[init_end]=$EPOCHREALTIME
}

__zpack_init $@
