#!/usr/bin/env zsh

# Output Functions
function __zpack_out() {
    print -P "%B%F{green}\u276F %f%b""$*"
}

function __zpack_out_plain() {
    print "$*"
}

function __zpack_warn() {
    print -P %F{yellow}"$*"%f >&2
}

function __zpack_err() {
    print -P %F{red}"$*"%f >&2
}

function __zpack_subcmd() {
    local prefix=$1
    local subcmd=$2

    if [[ -z $prefix ]]; then
        __zpack_err "No prefix given."
        return 1
    fi
    if [[ -z $subcmd ]]; then
        __zpack_err "No subcommand given."
        return 1
    fi
    if [[ ${+functions[__zpack_${prefix}_${subcmd}]} == 0 ]]; then
        __zpack_err "Invalid subcomand given."
        return 1
    fi
    shift 2
    __zpack_${prefix}_${subcmd} $@
}

function __zpack_apply() {
    unfunction __zpack_apply

    if [[ -n ${ZPACK[cache]} ]]; then
        unset "ZPACK[cache]"
        source ${ZPACK[cache_file]}
        ZPACK[cache_hit]=1
    fi
    __zpack_completion apply
    ZPACK[applied]=1
}

# Init function
function __zpack_init() {
    unfunction __zpack_init

    if ! zmodload zsh/datetime; then
        __zpack_err "Could not load zsh/datetime module."
        return 1
    fi

    ZPACK[stats_init_start]=$EPOCHREALTIME

    # Check cache
    if [[ -f ${ZPACK[cache_file]} && ${ZPACK[cache_file]} -nt ${ZDOTDIR:-$HOME}/.zshrc ]]; then
        ZPACK[cache]=1
    else
        autoload -Uz __zpack_cache

        # Init Variables
        typeset -ga __ZPACK_PLUGINS=()
        typeset -ga __ZPACK_RELEASES=()
        typeset -ga __ZPACK_SNIPPETS=()
        typeset -ga __ZPACK_BUNDLES=()
        typeset -ga __ZPACK_BINARIES=()

        # Setup directories and files
        local dir
        for dir in ${ZPACK[bin_dir]} ${ZPACK[cache_dir]}; do
            [[ -d $dir ]] || mkdir -p $dir
        done
        rm -f ${ZPACK[compdump_path]}
        : > ${ZPACK[cache_file]}
        __zpack_cache txt "#!/usr/bin/env zsh"
        __zpack_cache stats_time cache_start
        __zpack_cache path ${ZPACK[bin_dir]}

        # Zsh Plugin Standard
        typeset -g PMSPEC=0fbis
        __zpack_cache var PMSPEC
        __zpack_cache eval "typeset -ga zsh_loaded_plugins"
    fi

    # Define commands and their description
    typeset -gA __ZPACK_CMDS
    __ZPACK_CMDS=(
        "load"    "clone and load plugin"
        "omz"     "load ohmyzsh plugins or libraries"
        "bin"     "clone and add files to PATH"
        "release" "download and add binary release"
        "snippet" "download and load snippet from url"
        "bundle"  "load predefined bundles"
        "apply"   "apply setup to current shell"
        "update"  "update all repositories, reset and restart shell"
        "compile" "compile files from the given path"
        "clone"   "clone plugin from repository"
        "list"    "print plugins, releases, snippets, bundles and binaries"
        "reset"   "delete cache and bin dirs and restart shell"
        "prune"   "delete everything and restart shell"
        "stats"   "print statistics about zpack"
        "help"    "print usage information"
    )

    # Init completion
    autoload -Uz __zpack_completion && __zpack_completion init

    ZPACK[stats_init_end]=$EPOCHREALTIME
}

__zpack_init $@
