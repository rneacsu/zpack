#!/usr/bin/env zsh

function zpack-omz() {
    local completion location repo branch before_load after_load
    zparseopts -D -E -completion=completion \
        -before-load:=before_load -after-load:=after_load \
        || return 1

    if [[ $# == 0 ]]; then
        __zpack_err "'omz' requires at least one parameter:"
        __zpack_err "    zpack omz <lib or plugin path> [--completion]"
        return 1
    fi

    location=$1
    repo=${ZPACK_OMZ_REPO:-ohmyzsh/ohmyzsh}
    branch=$ZPACK_OMZ_BRANCH

    if [[ -z $ZSH ]]; then
        zpack-clone $repo $branch

        typeset -g ZSH="$(__zpack_dir plugin $repo $branch)"
        typeset -g ZSH_CACHE_DIR="$ZSH/cache"
        typeset -g ZSH_COMPLETIONS_DIR="$ZSH_CACHE_DIR/completions"
        [[ -d $ZSH_COMPLETIONS_DIR ]] || mkdir -p $ZSH_COMPLETIONS_DIR

        __zpack_cache var ZSH
        __zpack_cache var ZSH_CACHE_DIR
        __zpack_cache var ZSH_COMPLETIONS_DIR

        __zpack_cache fpath $ZSH_COMPLETIONS_DIR
    fi

    zpack-load $repo@$branch::$location $completion $before_load $after_load
}

zpack-omz $@
