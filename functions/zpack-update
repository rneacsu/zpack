#!/usr/bin/env zsh

function zpack-update() {
    local type=${1:-all}

    if [[ $# -gt 1 || $type != (all|self|plugins) ]]; then
        __zpack_err "'update' requires at most one parameter:"
        __zpack_err "    zpack update [all|self|plugins]"
        return 1
    fi

    if [[ $type == "all" || $type == "self" ]]; then
        if [[ -e "${ZPACK[dir]}/.git" ]]; then
            __zpack_out "Updating ZPack"
            git -C ${ZPACK[dir]} pull --ff-only
        fi
    fi

    if [[ $type == "all" || $type == "plugins" ]]; then
        local repo dir
        for repo in ${ZPACK[plugins_dir]}/**/.git(/F); do
            dir="${repo%/.git}"

            __zpack_out "Updating '${${dir#${ZPACK[plugins_dir]}/}:h}'"

            git -C $dir reset --hard --quiet
            git -C $dir clean -df --quiet
            git -C $dir pull  --ff-only
        done

        for version_file in ${ZPACK[releases_dir]}/**/.zpack_release_version(.); do
            rm "$version_file"
        done

        for snippet in ${ZPACK[snippets_dir]}/**/*(.); do
            rm "$snippet"
        done
    fi

    zpack-reset
}

zpack-update $@
